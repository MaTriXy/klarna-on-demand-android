apply plugin: 'maven'
apply plugin: 'signing'

// Add tasks for javadoc and source jar generation
task androidJavadocs(type: Javadoc) {
    exclude '**/jockeyjs/**'
    exclude '**/crypto/**'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.links("http://docs.oracle.com/javase/7/docs/api/")
    options.linksOffline("http://d.android.com/reference/", "${android.sdkDirectory}/docs/reference")
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

// Define upload task
signing {
    required { gradle.startParameter.taskNames.contains("uploadArchives") }
    sign configurations.archives
}

// Fallback values for OSSRH credentials
if (!project.hasProperty("ossrhUsername")) {
    ext.ossrhUsername = ""
}

if (!project.hasProperty("ossrhPassword")) {
    ext.ossrhPassword = ""
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.setArtifactId 'klarna-on-demand-sdk'

            pom.project {
                name 'klarna-on-demand-sdk'
                groupId = 'com.klarna.ondemand'
                version = "${android.defaultConfig.versionName}"
                packaging 'aar'
                description 'This package contains Klarna\'s on-demand purchase SDK for Android 4+.'
                url 'https://github.com/klarna/klarna-on-demand-android'

                scm {
                    url 'https://github.com/klarna/klarna-on-demand-android.git'
                    connection 'https://github.com/klarna/klarna-on-demand-android.git'
                    developerConnection 'https://github.com/klarna/klarna-on-demand-android.git'
                }

                developers {
                    developer {
                        id 'devs'
                        name 'Klarna InDeX team'
                        email 'index.e@klarna.com'
                    }
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

apply plugin: 'org.ajoberstar.github-pages'

githubPages {
  repoUri = 'https://github.com/klarna/klarna-on-demand-android.git'
  pages {
    from androidJavadocs.outputs.files
  }
}